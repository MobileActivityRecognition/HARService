/*
 * HARService: Activity Recognition Service
 * Copyright (C) 2015 agimenez
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *           http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'java'

task assembleDexJar(type: Jar, dependsOn: build) {
    from "$buildDir/classes/main/"
    baseName = "classes"
    exclude '**/WekaWrapperV1001.class'
}


import org.apache.tools.ant.taskdefs.condition.Os

task dexJar << {
    ext.androidSdkDir = System.env.ANDROID_HOME
    if(androidSdkDir == null) {
        Properties localProps = new Properties()
        localProps.load(new FileInputStream(file("../local.properties")))
        ext.androidSdkDir = localProps['sdk.dir']
    }
    // Make sure no existing jar file exists as this will cause dx to fail
    new File("${buildDir}/libs/WekaJ48DT.jar").delete();
    // Use command line dx utility to convert *.class files into classes.dex inside jar archive
    String cmdExt = Os.isFamily(Os.FAMILY_WINDOWS) ? '.bat' : ''
    exec {
        commandLine "${androidSdkDir}/build-tools/23.0.1/dx${cmdExt}", '--verbose', '--dex',
                "--output=${buildDir}/libs/WekaJ48DT.jar",
                "${buildDir}/libs/classes.jar"
    }
}

dexJar.dependsOn(assembleDexJar)
jar.finalizedBy(dexJar)

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
}